resource "aws_lambda_function" "create-transkribus-lambda-function" {

  s3_bucket     = var.lambda-jar-bucket
  s3_key        = var.enhancements-lambda-information[0].jar_path
  runtime       = var.enhancements-lambda-information[0].runtime
  timeout       = var.enhancements-lambda-information[0].timeout
  memory_size   = var.enhancements-lambda-information[0].memory
  role          = aws_iam_role.assume-lambda-role.arn
  layers        = concat([aws_lambda_layer_version.xslt-layer.arn], [aws_lambda_layer_version.enhancements-properties-layer.arn], [var.datadog-layer-1-arn, var.datadog-layer-2-arn])
  function_name = "${var.environment}-${var.enhancements-lambda-information[0].name}"
  handler       = var.enhancements-lambda-information[0].handler
  publish       = true

  environment {
    variables = {
      DD_API_KEY_SECRET_ARN = "datadog_api",
      DD_JMXFETCH_ENABLED   = false,
      DD_SITE               = "https://app.datadoghq.eu",
      DD_TRACE_ENABLED      = true,
      JAVA_TOOL_OPTIONS     = "-javaagent:\"/opt/java/lib/dd-java-agent.jar\" -XX:+TieredCompilation -XX:TieredStopAtLevel=1"
    }
  }
}

resource "aws_lambda_layer_version" "xslt-layer" {
  s3_bucket  = var.lambda-layer-bucket
  s3_key     = var.enhancements-lambda-layer-filepath
  layer_name = "${var.environment}-${var.enhancements-lambda-layer-name}"

  compatible_runtimes = [var.enhancements-lambda-information[0].runtime]
}

resource "aws_lambda_layer_version" "enhancements-properties-layer" {
  filename   = "${path.module}/zipped_properties_files/${var.environment}.properties.zip"
  layer_name = "${var.environment}-transkribus-properties"
  source_code_hash  = data.archive_file.zip_enhancements_properties_lambda_layer.output_base64sha256

  compatible_runtimes = [var.enhancements-lambda-information[0].runtime]
  depends_on = [data.archive_file.zip_enhancements_properties_lambda_layer]
}

data "archive_file" "zip_enhancements_properties_lambda_layer" {
  type        = "zip"
  output_path = "${path.module}/zipped_properties_files/${var.environment}.properties.zip"
  source_dir  = "${path.module}/properties_files/${var.environment}"

  # Without the `depends_on` argument, the zip file creation fails because the file to zip
  # doesn't exist on the local filesystem yet
  depends_on = [local_file.create-local-enhancements-lambda-properties-file]
}

resource "local_file" "create-local-enhancements-lambda-properties-file" {

  content = <<-EOT
    # This file is generated by Terraform, and shouldn't need to be modified manually.

    VERSION=${upper(var.environment)}
    DST_BUCKET=${var.environment}-${var.enhancements-destination-bucket-name}
    DST_EFS_ENABLED=false
    DST_EFS_PREFIX=
    DST_S3_PREFIX=${var.enhancements-dst-s3-prefix}
    DST_XSLT_OUTPUT_FOLDER=<ITEM_ID>/
    DST_XSLT_OUTPUT_SUFFIX=.xml
    TMP_DIR=${var.tmp-dir}
    XSLT=/opt/xslt/curious-cures.xslt
    XSLT_1_PARAMS=full_path_to_cudl_data_source:/tmp/${var.environment}-cudl-data-source/
    XSLT_S3_ITEM_RESOURCES=s3://${var.environment}-cudl-data-source/items/data/tei/<ITEM_ID>/<ITEM_ID>.xml
    REGION=${var.deployment-aws-region}
  EOT

  filename = "${path.module}/properties_files/${var.environment}/java/lib/cudl-loader-lambda.properties"
}

# Trigger lambda from the SQS queues
resource "aws_lambda_event_source_mapping" "enhancements-sqs-trigger-lambda" {
  count = length(var.enhancements-lambda-information)

  event_source_arn = aws_sqs_queue.enhancements-lambda-sqs-queue[count.index].arn
  function_name    = aws_lambda_function.create-transkribus-lambda-function.arn
}